SELECT DISTINCT ?langCode ?title ?qid ?collectionLabel 
?collectionTypePropLabel ?collectionTypeLabel ?description ?descriptionPropLabel ?wdItemPropLabel ?wdItem ?describedAtURLPropLabel 
?describedAtURL ?officialWebsitePropLabel ?officialWebsite ?creatorPropLabel ?creators ?maintainerPropLabel ?maintainers
?materialsStartPropLabel ?materialsEndPropLabel (YEAR(?materialsStart) as ?materialsStartYear) (YEAR(?materialsEnd) as ?materialsEndYear)
?digitizationStatusPropLabel ?digitizationStatusLabel ?accessStatusPropLabel ?accessStatusLabel 
?generalRecordsTypes ?generalRecordsPropLabel ?specificRecordsTypes ?specificRecordsPropLabel
?subjects ?subjectPropLabel ?languages ?languagePropLabel ?collectionTitle ?collectionTitlePropLabel 
?accessPolicy ?accessPolicyPropLabel ?projectStatus ?projectStatusPropLabel
?projectStartPropLabel ?projectEndPropLabel (YEAR(?projectStart) as ?projectStartYear) (YEAR(?projectEnd) as ?projectEndYear)
WHERE {
   ?collection wdt:P7 wd:Q85;
               wdt:P27 ?collectionTitle.
   BIND("Collections" as ?title).
   BIND("en" as ?langCode).
   BIND(REPLACE(STR(?collection), "http://ec2-34-227-69-60.compute-1.amazonaws.com/entity/", "") AS ?qid).
   OPTIONAL{?collection wdt:P50 ?collectionType.}
   OPTIONAL{?collection wdt:P37 ?description.
            FILTER(LANG(?description) = "en").}
   OPTIONAL{?collection wdt:P33 ?wdItem.}
   OPTIONAL{?collection wdt:P17 ?describedAtURL.}
   OPTIONAL{?collection wdt:P22 ?officialWebsite.}
   OPTIONAL{?collection wdt:P30 ?materialsStart.}
   OPTIONAL{?collection wdt:P31 ?materialsEnd.}
   OPTIONAL{?collection wdt:P40 ?digitizationStatus.}
   OPTIONAL{?collection wdt:P32 ?accessStatus.}
   OPTIONAL{?collection wdt:P41 ?accessPolicy.
            FILTER(LANG(?accessPolicy) = "en").}
   OPTIONAL{?collection wdt:P42 ?projectStatus.
            FILTER(LANG(?projectStatus) = "en").}
   OPTIONAL{?collection wdt:P43 ?projectStart.}
   OPTIONAL{?collection wdt:P44 ?projectEnd.}
   SERVICE wikibase:label { bd:serviceParam wikibase:language "en". 
      ?collection rdfs:label ?collectionLabel.
      ?collectionTitleProp rdfs:label ?collectionTitlePropLabel.
      ?collectionType rdfs:label ?collectionTypeLabel.
      ?wdItemProp rdfs:label ?wdItemPropLabel.
      ?describedAtURLProp rdfs:label ?describedAtURLPropLabel.
      ?officialWebsiteProp rdfs:label ?officialWebsitePropLabel.
      ?creatorProp rdfs:label ?creatorPropLabel.
      ?maintainerProp rdfs:label ?maintainerPropLabel.
      ?materialsStartProp rdfs:label ?materialsStartPropLabel.
      ?materialsEndProp rdfs:label ?materialsEndPropLabel.
      ?descriptionProp rdfs:label ?descriptionPropLabel.
      ?collectionTypeProp rdfs:label ?collectionTypePropLabel.
      ?digitizationStatus rdfs:label ?digitizationStatusLabel.
      ?digitizationStatusProp rdfs:label ?digitizationStatusPropLabel.
      ?accessStatus rdfs:label ?accessStatusLabel.
      ?accessStatusProp rdfs:label ?accessStatusPropLabel.
      ?generalRecordsProp rdfs:label ?generalRecordsPropLabel.
      ?specificRecordsProp rdfs:label ?specificRecordsPropLabel.
      ?subjectProp rdfs:label ?subjectPropLabel.
      ?languageProp rdfs:label ?languagePropLabel.
      ?accessPolicyProp rdfs:label ?accessPolicyPropLabel.
      ?projectStatusProp rdfs:label ?projectStatusPropLabel.
      ?projectStartProp rdfs:label ?projectStartPropLabel.
      ?projectEndProp rdfs:label ?projectEndPropLabel.
   }
   ?wdItemProp wikibase:directClaim wdt:P33.
   ?describedAtURLProp wikibase:directClaim wdt:P17.
   ?officialWebsiteProp wikibase:directClaim wdt:P22.
   ?creatorProp wikibase:directClaim wdt:P28.
   ?maintainerProp wikibase:directClaim wdt:P11.
   ?materialsStartProp wikibase:directClaim wdt:P30.
   ?materialsEndProp wikibase:directClaim wdt:P31.
   ?descriptionProp wikibase:directClaim wdt:P37.
   ?collectionTypeProp wikibase:directClaim wdt:P50.
   ?digitizationStatusProp wikibase:directClaim wdt:P40.
   ?accessStatusProp wikibase:directClaim wdt:P32.
   ?generalRecordsProp wikibase:directClaim wdt:P38.
   ?specificRecordsProp wikibase:directClaim wdt:P48.
   ?subjectProp wikibase:directClaim wdt:P23.
   ?languageProp wikibase:directClaim wdt:P13.
   ?collectionTitleProp wikibase:directClaim wdt:P27.
   ?accessPolicyProp wikibase:directClaim wdt:P41.
   ?projectStatusProp wikibase:directClaim wdt:P42.
   ?projectStartProp wikibase:directClaim wdt:P43.
   ?projectEndProp wikibase:directClaim wdt:P44.
   FILTER(LANG(?collectionTitle) = "en").
   {
     SELECT ?innerCollection 
            (group_concat(distinct ?generalRecordsLabel; separator="; ") as ?generalRecordsTypes)
            (group_concat(distinct ?specificRecordsLabel; separator="; ") as ?specificRecordsTypes)
            (group_concat(distinct ?subjectLabel; separator="; ") as ?subjects)
            (group_concat(distinct ?languageLabel; separator="; ") as ?languages)
            (group_concat(distinct ?creatorLink; separator="; ") as ?creators)
            (group_concat(distinct ?maintainerLink; separator="; ") as ?maintainers)
     WHERE {
       ?innerCollection wdt:P7 wd:Q85.
       OPTIONAL{?innerCollection wdt:P38 ?generalRecords}
       OPTIONAL{?innerCollection wdt:P48 ?specificRecords}
       OPTIONAL{?innerCollection wdt:P23 ?subject}
       OPTIONAL{?innerCollection wdt:P13 ?language}
       OPTIONAL{?innerCollection wdt:P28 ?creator.
         BIND(REPLACE(STR(?creator), "http://ec2-34-227-69-60.compute-1.amazonaws.com/entity/", "") AS ?creatorQid).
         ?creator rdfs:label ?creatorLabel.
         FILTER(LANG(?creatorLabel) = "en").
         BIND(CONCAT("<a href='./", STR(?creatorQid), ".html'>", STR(?creatorLabel), "</a>") AS ?creatorLink).
       }
       OPTIONAL{?innerCollection wdt:P11 ?maintainer.
        BIND(REPLACE(STR(?maintainer), "http://ec2-34-227-69-60.compute-1.amazonaws.com/entity/", "") AS ?maintainerQid).
        ?maintainer rdfs:label ?maintainerLabel.
        FILTER(LANG(?maintainerLabel) = "en").
        BIND(CONCAT("<a href='./", STR(?maintainerQid), ".html'>", STR(?maintainerLabel), "</a>") AS ?maintainerLink).
       }
       SERVICE wikibase:label { bd:serviceParam wikibase:language "en". 
          ?generalRecords rdfs:label ?generalRecordsLabel.
          ?specificRecords rdfs:label ?specificRecordsLabel. 
          ?subject rdfs:label ?subjectLabel.
          ?language rdfs:label ?languageLabel.            
       }
      }
     GROUP BY ?innerCollection
    }
  FILTER(?innerCollection = ?collection)
} ORDER BY (?collectionLabel)