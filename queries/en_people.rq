SELECT ?langCode ?title ?qid ?personLabel ?personDescription ?createdCollections ?foundedOrganizations ?maintainsCollections
WHERE {
   ?person wdt:P7 wd:Q21.
   BIND("People" as ?title).
   BIND("en" as ?langCode).
   BIND(REPLACE(STR(?person), "http://ec2-34-227-69-60.compute-1.amazonaws.com/entity/", "") AS ?qid).
   SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
   OPTIONAL{
        ?person schema:description ?personDescription.
        FILTER(LANG(?personDescription) = "en").}
   {
     SELECT ?innerPerson
            (group_concat(distinct ?createdCollectionLink; separator="; ") as ?createdCollections)
            (group_concat(distinct ?foundedOrgLink; separator="; ") as ?foundedOrganizations)
            (group_concat(distinct ?maintainsCollectionLink; separator="; ") as ?maintainsCollections)
     WHERE {
       ?innerPerson wdt:P7 wd:Q21.
       OPTIONAL{?innerPerson ^wdt:P28 ?createdCollection.
               BIND(REPLACE(STR(?createdCollection), "http://ec2-34-227-69-60.compute-1.amazonaws.com/entity/", "") AS ?createdCollectionQid).
               ?createdCollection rdfs:label ?createdCollectionLabel.
               FILTER(LANG(?createdCollectionLabel) = "en").
               BIND(CONCAT("<a href='./", STR(?createdCollectionQid), ".html'>", STR(?createdCollectionLabel), "</a>") AS ?createdCollectionLink)}
       OPTIONAL{?innerPerson ^wdt:P10 ?foundedOrganization.
               BIND(REPLACE(STR(?foundedOrganization), "http://ec2-34-227-69-60.compute-1.amazonaws.com/entity/", "") AS ?foundedOrgQid).
               ?foundedOrganization rdfs:label ?foundedOrgLabel.
               FILTER(LANG(?foundedOrgLabel) = "en").
               BIND(CONCAT("<a href='./", STR(?foundedOrgQid), ".html'>", STR(?foundedOrgLabel), "</a>") AS ?foundedOrgLink)}
       OPTIONAL{?innerPerson ^wdt:P11 ?maintainsCollection.
               BIND(REPLACE(STR(?maintainsCollection), "http://ec2-34-227-69-60.compute-1.amazonaws.com/entity/", "") AS ?maintainsCollectionQid).
               ?maintainsCollection rdfs:label ?maintainsCollectionLabel.
               FILTER(LANG(?maintainsCollectionLabel) = "en").
               BIND(CONCAT("<a href='./", STR(?maintainsCollectionQid), ".html'>", STR(?maintainsCollectionLabel), "</a>") AS ?maintainsCollectionLink)
               }
     } GROUP BY ?innerPerson 
   }
  filter(?innerPerson = ?person)
} ORDER BY (?personLabel)